/*buildscript {
 repositories {
 mavenCentral()
 }
 }*/

plugins {
    id 'java'
    id 'idea'
    id 'application'

    id 'com.diffplug.spotless' version '6.10.0'
    id 'com.github.spotbugs' version '5.0.12'
    id 'org.gradle.test-retry' version '1.4.1'

    // Fix guava -jre vs -android dependency resolution.
    // The terra-cloud-resource library requires the -jre guava version. Without this plugin, we may
    // use the wrong guava version, leading to runtime errors.
    // See also https://blog.gradle.org/guava
    id 'de.jjohannes.missing-metadata-guava' version '31.1.1'
}

// The tools/publish-release.sh script depends on this version string being of the format "version = '1.2.3'"
//version = '0.242.0'
version = 'dexamundsen-1932'
group = 'terra-cli'
//sourceCompatibility = JavaVersion.VERSION_17
//targetCompatibility = JavaVersion.VERSION_17

// If true, search local repository (~/.m2/repository/) first for dependencies.
def useMavenLocal = false
repositories {
    if (useMavenLocal) {
        mavenLocal() // must be listed first to take effect
    }
    mavenCentral()
    maven {
        url 'https://broadinstitute.jfrog.io/broadinstitute/libs-release/'
    }
    maven {
        url 'https://broadinstitute.jfrog.io/broadinstitute/libs-snapshot-local/'
    }
}

dependencies {
    // Needed for Mac M1
    implementation group: 'net.java.dev.jna', name: 'jna', version: '5.12.1'

    // Terra deps
    // TODO(PF-1928) - update clients after their JDK updates
    implementation group: 'bio.terra', name: 'datarepo-client', version: '1.391.0-SNAPSHOT'
    implementation group: 'bio.terra', name: 'externalcreds-client-resttemplate', version: '0.98.0-SNAPSHOT'
    implementation group: 'bio.terra', name: 'terra-resource-janitor-client', version: '0.114.0-SNAPSHOT'
    implementation group: 'bio.terra', name: 'workspace-manager-client', version: '0.254.324-SNAPSHOT'
    implementation group: 'org.broadinstitute.dsde.workbench', name: 'sam-client_2.13', version: '0.1-ffb0a89-SNAP'

    implementation platform('bio.terra.cloud-resource-lib:platform:0.11.0')
    implementation group: 'bio.terra.cloud-resource-lib', name: 'google-cloudresourcemanager', version: '1.4.0'
    implementation group: 'bio.terra.cloud-resource-lib', name: 'google-bigquery', version: '0.13.0'
    implementation group: 'bio.terra.cloud-resource-lib', name: 'google-notebooks', version: '0.10.0'
    implementation group: 'bio.terra.cloud-resource-lib', name: 'google-storage', version: '0.14.0'

    // hk2 is required to use WSM client, but not correctly exposed by the client
    implementation group: 'org.glassfish.jersey.inject', name: 'jersey-hk2', version: '2.36' // TODO

    // command parsing
    implementation group: 'info.picocli', name: 'picocli', version: '4.6.3'

    // logging
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.4.0'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.0'

    // serialization
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.13.4'
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.13.4'

    // GCP
    implementation group: 'com.google.auth', name: 'google-auth-library-credentials', version: '1.11.0'
    implementation group: 'com.google.auth', name: 'google-auth-library-oauth2-http', version: '1.11.0'
    implementation group: 'com.google.oauth-client', name: 'google-oauth-client-java6', version: '1.34.1'
    implementation group: 'com.google.oauth-client', name: 'google-oauth-client-jetty', version: '1.34.1'
    implementation group: 'com.google.cloud', name: 'google-cloud-bigquery', version: '2.15.0'
    implementation group: 'com.google.cloud', name: 'google-cloud-storage', version: '2.11.3'

    // Docker
    implementation group: 'com.github.docker-java', name: 'docker-java-core', version: '3.2.13'
    implementation group: 'com.github.docker-java', name: 'docker-java-transport-httpclient5', version: '3.2.13'

    // static analysis
    compileOnly group: 'com.github.spotbugs', name: 'spotbugs-annotations', version: '4.7.2'
    testCompileOnly group: 'com.github.spotbugs', name: 'spotbugs-annotations', version: '4.7.2'

    // Deps whose versions are controlled by Spring
    implementation group: 'org.apache.commons', name: 'commons-text', version: '1.9'
    implementation group: 'org.apache.commons', name: 'commons-lang3'

    // Swagger deps
    implementation group: 'io.swagger.core.v3', name: 'swagger-annotations', version: '2.2.2'

    // Test deps
    testImplementation platform('org.junit:junit-bom:5.9.0')
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.9.0'
    testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '2.2'

    // PubSub is used in tests to communicate with Janitor, but is not required for the CLI itself.
    testImplementation group: 'com.google.cloud', name: 'google-cloud-pubsub', version: '1.120.13'

    annotationProcessor group: 'info.picocli', name: 'picocli-codegen', version: '4.6.3'
}

// for scans
if (hasProperty('buildScan')) {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}

apply from: "$rootDir/gradle/application.gradle"
apply from: "$rootDir/gradle/dependency-locking.gradle"
apply from: "$rootDir/gradle/spotbugs.gradle"
apply from: "$rootDir/gradle/spotless.gradle"
apply from: "$rootDir/gradle/tools.gradle"
apply from: "$rootDir/gradle/test.gradle"

compileJava.dependsOn tasks.spotlessApply
defaultTasks 'spotlessApply', 'build'