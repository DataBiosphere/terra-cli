plugins {
    id 'java'
    id 'idea'
    id 'application'

    id 'com.diffplug.spotless' version '6.10.0'
    id 'com.github.spotbugs' version '5.0.12'
    id 'org.gradle.test-retry' version '1.4.1'

    // Fix guava -jre vs -android dependency resolution.
    // The terra-cloud-resource library requires the -jre guava version. Without this plugin, we may
    // use the wrong guava version, leading to runtime errors.
    // See also https://blog.gradle.org/guava
    id 'de.jjohannes.missing-metadata-guava' version '31.1.1'
}

// TODO(PF-1981) - Move version to settings.gradle
// The tools/publish-release.sh script depends on this version string being of the format "version = '1.2.3'"
version = '0.287.0'
group = 'terra-cli'

// If true, search local repository (~/.m2/repository/) first for dependencies.
def useMavenLocal = false
repositories {
    if (useMavenLocal) {
        mavenLocal() // must be listed first to take effect
    }
    mavenCentral()
    maven {
        url 'https://broadinstitute.jfrog.io/broadinstitute/libs-release-local/'
    }
    maven {
        url 'https://broadinstitute.jfrog.io/broadinstitute/libs-snapshot-local/'
    }
}

dependencies {
    // Needed for Mac M1
    implementation group: 'net.java.dev.jna', name: 'jna', version: '5.12.1'

    // Terra deps
    // TODO(PF-1928) - update versions after Terra-client JDK updates
    implementation group: 'bio.terra', name: 'datarepo-client', version: '1.0.155-SNAPSHOT'
    implementation group: 'bio.terra', name: 'externalcreds-client-resttemplate', version: '0.82.0-SNAPSHOT'
    implementation group: 'bio.terra', name: 'terra-resource-janitor-client', version: '0.113.5-SNAPSHOT'
    implementation group: 'bio.terra', name: 'workspace-manager-client', version: '0.254.468-SNAPSHOT'
    implementation group: 'org.broadinstitute.dsde.workbench', name: 'sam-client_2.13', version: '0.1-ffb0a89-SNAP'

    // TODO(PF-1928) - update versions after Terra-client JDK updates
    implementation platform('bio.terra.cloud-resource-lib:platform:0.2.0')
    implementation group: 'bio.terra.cloud-resource-lib', name: 'google-cloudresourcemanager'
    implementation group: 'bio.terra.cloud-resource-lib', name: 'google-bigquery'
    implementation group: 'bio.terra.cloud-resource-lib', name: 'google-notebooks'
    implementation group: 'bio.terra.cloud-resource-lib', name: 'google-storage'

    // hk2 is required to use WSM client, but not correctly exposed by the client
    implementation group: 'org.glassfish.jersey.inject', name: 'jersey-hk2', version: '2.36'

    // command parsing
    implementation group: 'info.picocli', name: 'picocli', version: '4.6.3'

    // logging
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.4.0'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.0'

    // serialization
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.13.4'
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.13.4'

    // GCP
    // TODO(PF-1928) - update versions after Terra-client JDK updates
    implementation group: 'com.google.auth', name: 'google-auth-library-credentials', version: '1.6.0'
    implementation group: 'com.google.auth', name: 'google-auth-library-oauth2-http', version: '1.6.0'
    implementation group: 'com.google.oauth-client', name: 'google-oauth-client-java6', version: '1.33.3'
    implementation group: 'com.google.oauth-client', name: 'google-oauth-client-jetty', version: '1.33.3'
    implementation group: 'com.google.cloud', name: 'google-cloud-bigquery', version: '1.116.0'
    implementation group: 'com.google.cloud', name: 'google-cloud-storage', version: '2.7.2'

    // Docker
    implementation group: 'com.github.docker-java', name: 'docker-java-core', version: '3.2.13'
    implementation group: 'com.github.docker-java', name: 'docker-java-transport-httpclient5', version: '3.2.13'

    // static analysis
    compileOnly group: 'com.github.spotbugs', name: 'spotbugs-annotations', version: '4.7.2'
    testCompileOnly group: 'com.github.spotbugs', name: 'spotbugs-annotations', version: '4.7.2'

    // Deps whose versions are controlled by Spring
    implementation group: 'org.apache.commons', name: 'commons-text', version: '1.9'
    implementation group: 'org.apache.commons', name: 'commons-lang3'

    // Swagger deps
    implementation group: 'io.swagger.core.v3', name: 'swagger-annotations', version: '2.2.2'

    // Test deps
    testImplementation platform('org.junit:junit-bom:5.9.0')
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.9.0'
    testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '2.2'

    // PubSub is used in tests to communicate with Janitor, but is not required for the CLI itself.
    testImplementation group: 'com.google.cloud', name: 'google-cloud-pubsub', version: '1.120.13'

    annotationProcessor group: 'info.picocli', name: 'picocli-codegen', version: '4.6.3'
}

// for scans
if (hasProperty('buildScan')) {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}

compileJava.dependsOn tasks.spotlessApply

apply from: "$rootDir/gradle/application.gradle"
apply from: "$rootDir/gradle/dependency-locking.gradle"
apply from: "$rootDir/gradle/spotbugs.gradle"
apply from: "$rootDir/gradle/spotless.gradle"
apply from: "$rootDir/gradle/tools.gradle"
apply from: "$rootDir/gradle/test.gradle"
